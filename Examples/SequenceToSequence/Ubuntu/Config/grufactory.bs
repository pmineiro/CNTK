# NB: lots of redundant dropout nodes removed, may impact results ...

GRUPreviousHC (gruState) = [
   h = BS.Loop.Previous (gruState.h)         // hidden state(t-1)
]

GRUNextHC (gruState) = [
   h = BS.Loop.Next (gruState.h)             // hidden state(t+1)
]

GRUInstantiate (f, x, prevState) = [
  _privateInnards = [
     htprev = prevState.h

     z = Sigmoid (f.Zoo * htprev + f.Zoi * x + f.Zo)        
     r = Sigmoid (f.Roo * htprev + f.Roi * x + f.Ro)
     htilde = Tanh (f.Hoo * (r .* htprev) + f.Hoi * x + f.Ho)

     deltaht = (Constant (1) - z) .* (htilde - htprev)

     ht = htprev + if f.addZoneout then Dropout (deltaht) else deltaht
  ]

  h = _privateInnards.ht
  dim = f.outputDim
]

RecurrentGRUInstantiate (factory, x, previousHook) = [
  prevState = previousHook (gruState)
  gruState = GRUInstantiate (factory, x, prevState)
].gruState

RecurrentGRUFactory (outputDim,
                     inputDim,
                     addDropout=false,
                     addZoneout=false) = [
  addZoneout1 = addZoneout ; addDropout1 = addDropout

  Woo() = BS.Parameters.WeightParam (outputDim, outputDim)
  Woi() = BS.Parameters.WeightParam (outputDim, inputDim)
  Bo () = BS.Parameters.BiasParam (outputDim)

  res = [
     Zoo = Woo (); Zoi = Woi (); Zo = Bo ()
     Roo = Woo (); Roi = Woi (); Ro = Bo ()
     Hoo = Woo (); Hoi = Woi (); Ho = Bo ()

     inputDim = inputDim
     outputDim = outputDim
     addZoneout = addZoneout1
     addDropout = addDropout1
  ]
].res

RecurrentBiresidualGRUStackFactory (layerDims, 
                                    inputDim, 
                                    addDropout=false,
                                    addZoneout=false) = [
  layerDims1 = layerDims ; inputDim1 = inputDim
  addZoneout1 = addZoneout ; addDropout1 = addDropout

  res = [
    factories[i:0..Length (layerDims)-1] = [
      vDim = if i == 0 then inputDim else factories[i-1].dim

      fwd = RecurrentGRUFactory (layerDims[i],
                                 vDim,
                                 addDropout=addDropout1,
                                 addZoneout=addZoneout1)
      bwd = RecurrentGRUFactory (layerDims[i], 
                                 vDim,
                                 addDropout=addDropout1,
                                 addZoneout=addZoneout1)
      dim = layerDims[i]
    ]

    layerDims = layerDims1
    inputDim = inputDim1
  ]
].res

RecurrentBiresidualGRUStackInstantiate (f, input) = [
  layers[i:0..Length (f.layerDims)-1] = [
    dropInput = if i == 0 
                then if f.addDropout 
                     then Dropout (input) 
                     else input 
                else layers[i-1].h

    fwd = RecurrentGRUInstantiate (f.factories[i].fwd,
                                   dropInput,
                                   GRUPreviousHC)

    bwd = RecurrentGRUInstantiate (f.factories[i].bwd,
                                   dropInput,
                                   GRUNextHC)

    dropfh = if f.addDropout then Dropout (fwd.h) else fwd.h
    dropbh = if f.addDropout then Dropout (bwd.h) else bwd.h
    h = dropfh + dropbh + dropInput
    dim = f.layerDims[i] 
  ]
].layers
